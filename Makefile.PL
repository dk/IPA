use strict;
use Config;
require 5.00502;
use File::Find;
use File::Basename;
use File::Path;
use File::Copy;
use Cwd;
use DynaLoader;
use Prima::Gencls;

my $PRJ = 'IPA';

use vars qw( %make_trans @ovvars $dir_sep $path_sep );
use vars @ovvars = qw(
    $CC $CFLAGS $CDLFLAGS $COUTOFLAG $COUTEXEFLAG $CINCPATHFLAG
    $CDEFFLAG $COMPONLYFLAG $CLIBPATHFLAG $CLINKPREFIX
    $LD $LDFLAGS $LDOUTFLAG $LDLIBPATHFLAG $LDLIBFLAG
    $OBJ_EXT $LIB_EXT $EXE_EXT $DL_EXT $SCRIPT_EXT
    @INCPATH
    @LIBPATH
    @LIBS
    $PLATFORM
    $COMPILER
    $PREFIX
    $PREFIX_BIN
    $INSTALL_BIN
    $INSTALL_LIB
    $INSTALL_DL
    $INSTALL_EXAMPLES
    $TMPDIR
    $NULLDEV
    $MAKE
    $RM
    $SHQUOTE
    $MAKETYPE
    $DEFFILE
);

use vars qw(
    $PRIMA_INSTDIR
    $PRIMA_HEADERS
    $CWD
    $Win32
    $OS2
    $OS2DLLF
    $useGC
    %cc_specs
    %DEFINES
    %USER_VARS
    %USER_VARS_ADDONS
    %USER_VARS_LINKS
    %USER_DEFINES
    %USER_DEFINES_ADDONS
    %overrideable
    $ARGV_STR
    @allclean
    @allrealclean
    @allobjects
    %alldeps
    %mapheader
    @allinstall
    @allbins
    @alldirs
    @allsubtargets
    @subtargetsdirs
    @headers
    @footers
    @Makefile_deps
);

sub qd
{
    my ( $path_str) = @_;
    $path_str =~ s[/][$dir_sep]g;
    return $path_str;
}

sub dl_name
{
   my $name = $_[0];
   $name = &DynaLoader::mod2fname([$name]) if $OS2 && defined &DynaLoader::mod2fname;
   return "$name$DL_EXT";
}


sub quoted_split
{
   my @neww = ();
   my $text = shift;
   while ($text =~ m<
       ("[^\"\\]*(?:\\.[^\"\\]*)*")\s*
     | (\S+)\s*
     | \s+
   >gx) {
          push @neww, $+ if defined $+;
   }
   return @neww;
}

sub fatal
{
    open ERRLOG, ">". qd "$CWD/Makefile.err";
    print ERRLOG @_;
    close ERRLOG;
    die @_;
}

sub quotemake
{
    return join '', map { $make_trans{ $MAKETYPE}->[ ord]} split //, $_[ 0];
}

sub tempfile
{
    my $mask = shift;
    my $name;
    my $n = 0;
    do {
        $name = sprintf $mask, $n++;
    } while ( -e $name);
    return $name;
}

sub cc_command_line
{
    my ( $srcf, $objf, $exef, $compile_only, $dl) = @_;
    my @cc = ( $CC, split ' ', $CFLAGS);
    push @cc, split ' ', $CDLFLAGS if $dl;
    push @cc, $COMPONLYFLAG if $compile_only;
    push @cc, map( { "$CINCPATHFLAG$_"} @INCPATH);
    for ( keys %DEFINES) {
       next unless $DEFINES{$_};
       push @cc, "$CDEFFLAG$_=$DEFINES{$_}";
    }
    push @cc, "${ CDEFFLAG}HAVE_CONFIG_H=1";
#   push @cc, "${CDEFFLAG}PRIGRAPH" if $usePrigraph;
    push @cc, ( $compile_only ? "$COUTOFLAG$objf" : "$COUTEXEFLAG$exef");
    push @cc, "$COUTOFLAG$objf" if $Win32 && ! $compile_only;
    push @cc, map { "$CLIBPATHFLAG$_"} @LIBPATH unless $compile_only || ( $COMPILER eq 'msvc');
    push @cc, $srcf;
    return @cc if $compile_only;
    push @cc, $CLINKPREFIX;
    push @cc, map { "$CLIBPATHFLAG$_"} @LIBPATH if $COMPILER eq 'msvc';
    push @cc, map { "$LDLIBFLAG$_"} @LIBS;
    return @cc;
}

sub ld_command_line
{
    my ( $dstf, $deffile) = (shift,shift);
    my @ld = ( $LD, split( ' ', $LDFLAGS));
    if ( $COMPILER eq 'bcc32') {
        push @ld, '-L"' . join( ';', @LIBPATH) . '" c0d32.obj ';
        push @ld, @_;
    } 
    else {
        push @ld, map { "$LDLIBPATHFLAG$_"} @LIBPATH;
        push @ld, "$LDOUTFLAG$dstf", @_;
        push @ld, map { "$LDLIBFLAG$_"} @LIBS;
    }
    if ( $Win32) {
       if ( $COMPILER eq 'bcc32') { 
           push @ld, ",", $dstf, ", ,", @LIBS, ",", "$deffile,";
       } else {
           push @ld, "/DEF:$deffile";
       }
    } elsif ( $OS2) {
       push @ld, $deffile; 
    }
    return @ld;
}

sub null_output
{
    open OLDSTDOUT, ">&STDOUT" or die "STDOUT dup failed: $!";
    open OLDSTDERR, ">&STDERR" or die "STDERR dup failed: $!";
#   $NULLDEV = ( $Win32 || $OS2) ? "CON" : "/dev/tty";
    if ( $^O !~ /linux/) {
        close STDOUT;
        close STDERR;
    }
    open STDOUT, ">$NULLDEV" or fatal "STDOUT redirect failed: $!";
    open STDERR, ">&STDOUT" or fatal "STDERR redirect failed: $!";
}

sub restore_output
{
    if ( $^O !~ /linux/) {
        close STDOUT;
        close STDERR;
    }
    open STDOUT, ">&OLDSTDOUT" or fatal "STDOUT restoration failed: $!";
    open STDERR, ">&OLDSTDERR" or fatal "STDERR restoration failed: $!";
    close OLDSTDOUT;
    close OLDSTDERR;
}

sub compile
{
    my ( $text, $compile_only, @extra) = @_;
    my $tmpsrc = qd( tempfile( "$TMPDIR/pmts%04d.c"));
    my $tmpo = qd( tempfile( "$TMPDIR/pmts%04d$OBJ_EXT"));
    my $tmpexe = qd( tempfile( "$TMPDIR/pmts%04d$EXE_EXT"));
    my @tmpextras = (
       qd( tempfile( "$TMPDIR/pmts%04d.ilk")),
       qd( tempfile( "$TMPDIR/pmts%04d.pdb")),
    );

    open TMPSRC, ">$tmpsrc" or die "Creation of temporary file $tmpsrc failed";
    print TMPSRC $text;
    close TMPSRC;

    null_output;
    my @cc = grep !/^-W/i, cc_command_line( $tmpsrc, $tmpo, $tmpexe, $compile_only || 0);
    push @cc, @extra;
    print STDERR "@cc\n";
    my $rc = system "@cc";
    restore_output;
    unlink $tmpsrc;
    unlink $tmpo if -w $tmpo;
    unlink $tmpexe if -w $tmpexe;
    unlink $_ for @tmpextras;
    return( $rc == 0);
}

sub have_header
{
    my $header = shift;
    (my $defname = "HAVE_" . uc $header) =~ s/\W/_/g;
    return $DEFINES{$defname} if exists $DEFINES{$defname};
    my @pre_headers = map { "#include <$_>\n" } @_;
    print "Checking for presence of $header... ";
    my $present = compile( <<EOF, 1);
@pre_headers
#include <$header>
EOF
    $DEFINES{ $defname} = undef;
    $DEFINES{ $defname} = 1 if $present;
    print( $present ? "yes" : "no", "\n");
    return $present;
}

sub find_header
{
    my $header = shift;
    my ( $incpath, $present);
    foreach $incpath ( @_) {
        local @INCPATH = @INCPATH;
        push @INCPATH, $incpath if $incpath;
        $present = compile( <<EOF, 1);
#include <$header>
EOF
        return $incpath if $present;
    }
    return undef;
}

sub find_lib
{
    my $lib = shift;
    my ( $libpath, $present);
    local @LIBS = @LIBS;
    push @LIBS, $lib;
    foreach $libpath ( @_) {
        local @LIBPATH = (@LIBPATH, $libpath) if $libpath;
        $present = compile( <<EOF);
int
main()
{
   return 0;
}
EOF
        return $libpath if $present;
    }
    return undef;
}

sub have_func
{
    my ( $funcname, @headers) = @_;
    die "have_func() without any header is deprecated" unless @headers;
    my $defname = "HAVE_" . uc $funcname;
    my @srchead = map { "#include <$_>\n"} @headers;
    $defname =~ s/\W/_/g;
    print "Checking for function $funcname... ";
    my $rc = compile( <<EOF);
@srchead

int
main()
{
    void * ixi = $funcname;
    return 0;
}
EOF
    if ( $rc) {
        $DEFINES{ $defname} = 1;
        print "yes\n";
    } else {
        $DEFINES{ $defname} = undef;
        print "no\n";
    }
    return $rc;
}

sub have_define
{
    my ( $defname) = @_;
    my $cwd = cwd;
    chdir $TMPDIR;
    my $tmpsrc = qd( tempfile( "pmts%04d.c"));
    my $tmpo = $tmpsrc;
    if ( $Win32) {
        $tmpo =~ s/\.c$/\.obj/;
    }
    else {
        $tmpo =~ s/\.c$/\.o/;
    }
    open TMPSRC, ">$tmpsrc" or die "Creation of temporary file $tmpsrc failed";
    print TMPSRC <<EOF;
int
main()
{
#if defined( $defname)
return 0;
#else
#error No
#endif
}
EOF
    close TMPSRC;
    null_output;
    my $ccrc = system( "$CC -c $tmpsrc");
    restore_output;
    unlink $tmpsrc, $tmpo;
    chdir $cwd;
    return $ccrc == 0;
}

sub have_type
{
    my ( $type, @headers) = @_;
    (my $defname = "HAVE_" . uc $type) =~ s/\W/_/g;
    return 1 if $DEFINES{$defname};
    print "Checking for presence of type ${ type}... ";
    my @srchead = map { "#include <$_>\n"} @headers;
    my $rc = compile( <<EOF);
@srchead

int
main()
{
    ${ type} foo;
    return 0;
}
EOF
    if ( $rc) {
        $DEFINES{ $defname} = 1;
        print "yes\n";
    }
    else {
	$DEFINES{ $defname} = undef;
        print "no\n";
    }
    return $rc;
}

sub have_types_in
{
    my ($hdr, @types) = @_;
    return unless have_header( $hdr);
    for my $type (@types) {
       have_type( $type, $hdr);
    }
}

sub have_funcs_in
{
    my ($hdr, @funcs) = @_;
    return unless have_header( $hdr);
    for my $func (@funcs) {
       have_func( $func, $hdr);
    }
}


sub setvar
{
    my ( $varname) = shift;
    die "Tried to set non-overrideable variable $varname" unless $overrideable{ $varname};
    if ( $overrideable{ $varname} eq '$') {
        if ( defined $USER_VARS{ $varname}) {
            eval "\$$varname = \$USER_VARS{ \$varname}";
        }
	elsif ( defined $USER_VARS_LINKS{ $varname}
	        && exists $USER_VARS{ $USER_VARS_LINKS{ $varname}->{ src}}) {
	    eval "\$$varname = sprintf \"$USER_VARS_LINKS{ $varname}->{ format}\", \$$USER_VARS_LINKS{ $varname}->{ src}";
	}
        else {
            eval "\$$varname = join( '', \@_)";
        }
        die $@ if $@;
        if ( defined $USER_VARS_ADDONS{ $varname}) {
            eval "\$$varname .= join( '', \@{ \$USER_VARS_ADDONS{ \$varname}})";
        }
        die $@ if $@;
    }
    elsif ( $overrideable{ $varname} eq '@') {
        if ( defined $USER_VARS{ $varname}) {
            eval "\@$varname = \$USER_VARS{ \$varname}";
        }
        else {
            eval "\@$varname = \@_";
        }
        die $@ if $@;
        if ( defined $USER_VARS_ADDONS{ $varname}) {
            eval "push \@$varname, \@{ \$USER_VARS_ADDONS{ \$varname}}";
        }
        die $@ if $@;
    }
    else {
        die "Unsupported type of variable";
    }
}

sub env_true
{
    my ( $var) = @_;
    return( $ENV{ $var} && $ENV{ $var} =~ /^1|yes|on|true$/i);
}

sub check_dTHR
{
    print "Checking dTHR definition...";
    my $rc = compile( <<EOF, 1, "-Wunused", "-Werror");
#include <EXTERN.h>
#include <perl.h>

void
dummy(void)
{
   dTHR;
}
EOF
    if ( $rc) {
        print " okay\n";
    } else {
        $DEFINES{REMOVE_dTHR_FROM_dSP} = 1;
        print " hack needed\n";
    }
    return $rc;
}

sub setup_variables
{
    my $platform;
    if ( $Win32) {              # XXX Correct for NT.
        $platform = 'win32';
    }
    elsif ( $OS2) {
        $platform = 'os2';
    }
    else {
        $platform = 'unix';
    }
    setvar( 'PLATFORM', $platform);
    print "Locating Prima ...";

    if ( -f qd("$Config{installsitearch}/Prima/CORE/apricot.h")) {
       $PRIMA_INSTDIR  = $Config{installsitearch};
       $PRIMA_HEADERS = "Prima/CORE";
    } else {
       for (split( $path_sep, $ENV{PERL5LIB})) {
          next unless -f "$_/include/apricot.h";
          $PRIMA_INSTDIR = $_;
          $PRIMA_HEADERS = 'include';
       }
    }
    die "not found.\nPlease set \$PERL5LIB\n" unless defined $PRIMA_INSTDIR;
    push( @INCPATH, qd("$PRIMA_INSTDIR/$PRIMA_HEADERS"),
         qd("$PRIMA_INSTDIR/$PRIMA_HEADERS/generic"),
         $Config{installarchlib} . qd( "/CORE"),
         'include',
         qd('include/generic'),
       );
    $mapheader{'apricot.h'} = undef; 
    print "$INCPATH[0]\n";
    

    %cc_specs = (
                 gcc => {
                         warnflags => "-Wall -Werror",
                         cflags => "",
                         coutoflag => '-o',
                         coutexeflag => '-o',
                         cdebugflags => '-g -O2',
                         clinkprefix => '',
                         clibpathflag => '-L',
                         ldflags => '',
                         ldoutflag => '-o',
                         ldlibpathflag => '-L',
                         ldlibflag => '-l',
                         lddebugflags => '-g',
                         name => "GNU",
                        },
                 emx => {
                         warnflags => "-Wall -Werror",
                         cflags => "",
                         coutoflag => '-o',
                         coutexeflag => '-o',
                         cdebugflags => '-g -O2',
                         clinkprefix => '',
                         clibpathflag => '-L',
                         ldflags => '',
                         ldoutflag => '-o',
                         ldlibpathflag => '-L',
                         ldlibflag => '-l',
                         lddebugflags => '-g',
                         name => "EMX",
                        },
                 msvc => {
                          warnflags => "-W3 -WX",
                          cflags => "",
                          coutoflag => '-Fo',
                          coutexeflag => '-Fe',
                          clinkprefix => '/link',
                          clibpathflag => '/LIBPATH:',
                          cdebugflags => '-Zi',
                          ldflags => '',
                          ldoutflag => '/OUT:',
                          #ldlibpathflag => '/DEF:win32/Prima.def /LIBPATH:',
                          ldlibpathflag => '/LIBPATH:',
                          ldlibflag => '',
                          lddebugflags => '/DEBUG',
                          name => "Microsoft Visual C++",
                         },
                 bcc32 => {
                           warnflags => "-w0", # Borland is the only compiler which doesn't allow of geting rid of warnings.
                           cflags => "-tWM",
                           coutoflag => '-o',
                           coutexeflag => '-e',
                           clinkprefix => '',
                           clibpathflag => '-L',
                           cdebugflags => '-v -y',
                           ldflags => '',
                           ldoutflag => '',
                           ldlibpathflag => '-L',
                           ldlibflag => '',
                           lddebugflags => '-v',
                           name => "Borland C++",
                          },
                );

    setvar( 'TMPDIR', $ENV{ TMPDIR} || $ENV{ TEMPDIR} || ( $Win32 ? ( $ENV{ TEMP} || "$ENV{ SystemDrive}\\TEMP") : "/tmp"));
    setvar( 'NULLDEV', $Win32 ? "NUL" : "/dev/null");

    setvar( 'CC', $Config{ cc});

    print "Determining compiler type... ";
    if ( defined $USER_VARS{ COMPILER}) {
        $COMPILER = $USER_VARS{ COMPILER};
        die "Compiler type $COMPILER is unknown" unless defined $cc_specs{ $COMPILER};
    }
    elsif ( have_define( "__EMX__")) {
        $COMPILER = 'emx';
    }
    elsif ( have_define( "__GNUC__")) {
        $COMPILER = 'gcc';
    }
    elsif ( have_define( "__BORLANDC__")) {
        $COMPILER = 'bcc32';
    }
    elsif ( have_define( "_MSC_VER")) {
        $COMPILER = 'msvc';
    }
    else {
        die "Unknown or unsupported compiler\n";
    }
    print "$cc_specs{ $COMPILER}->{ name}\n";

    my $ccflags   = $Config{ ccflags};
    my $warnflags = $cc_specs{ $COMPILER}->{ warnflags};
    my $ldflags = $COMPILER eq 'emx' ? "" : "$Config{ ldflags} ";
    my $lddlflags = $Config{ lddlflags};
    if ( $COMPILER eq 'msvc' && (( $ccflags =~ /TP/) || ($ccflags =~ /O\d/))) { # M$VC++ protection
        $warnflags =~ s/W3/W1/;        # lower warning level
        $warnflags =~ s/(-|\/)WX//;    # and dismiss 'warnings as errors'; ( for its stupidity in the c++ case)
        $ldflags =~ s/(-|\/)nodefaultlib//g;
        $lddlflags =~ s/(-|\/)nodefaultlib//g;
    }
    setvar( 'CFLAGS',  "$ccflags $warnflags $cc_specs{ $COMPILER}->{ cflags}" . ( defined $ENV{ PRIMA_DEVEL} ? " $cc_specs{ $COMPILER}->{ cdebugflags}" : " " . ( $Config{ optimize} || '')) . " ");
    setvar( 'CDLFLAGS', "$Config{ cccdlflags} $Config{ ccdlflags}");
    setvar( 'COUTOFLAG', $cc_specs{ $COMPILER}->{ coutoflag});
    setvar( 'COUTEXEFLAG', $cc_specs{ $COMPILER}->{ coutexeflag});
    setvar( 'CINCPATHFLAG', '-I');
    setvar( 'CDEFFLAG', '-D');
    setvar( 'COMPONLYFLAG', '-c');
    setvar( 'CLINKPREFIX', $cc_specs{ $COMPILER}->{ clinkprefix});
    setvar( 'CLIBPATHFLAG', $cc_specs{ $COMPILER}->{ clibpathflag});
    setvar( 'LD', $Config{ ld});
    #setvar( 'LDFLAGS', "$ldflags $lddlflags $cc_specs{ $COMPILER}->{ ldflags}" . ( defined $ENV{ PRIMA_DEVEL} ? " $cc_specs{ $COMPILER}->{ lddebugflags}" : "") . " ");
    setvar( 'LDFLAGS', "$lddlflags $cc_specs{ $COMPILER}->{ ldflags}" . ( defined $ENV{ PRIMA_DEVEL} ? " $cc_specs{ $COMPILER}->{ lddebugflags}" : "") . " ");
    setvar( 'LDOUTFLAG', $cc_specs{ $COMPILER}->{ ldoutflag});
    setvar( 'LDLIBPATHFLAG', $cc_specs{ $COMPILER}->{ ldlibpathflag});
    setvar( 'LDLIBFLAG', $cc_specs{ $COMPILER}->{ ldlibflag});
    setvar( 'OBJ_EXT', $Config{ _o});
    setvar( 'LIB_EXT', $Config{ _a});
    setvar( 'EXE_EXT', $Config{ _exe});
    setvar( 'SCRIPT_EXT',
            ( $Win32 ? '.bat' : ( $OS2 ? '.cmd' : '')));
    setvar( 'DL_EXT', "." . $Config{ dlext});
    my @libpaths = quoted_split( $Config{ libpth});
    if ( $COMPILER eq 'msvc') {
       # explanation: link /LIBPATH:"Program Files" dirty fails, hail to M$ :E,
       # trying to link Files.obj with /LIBPATH:Program
       my %envlibs = map { lc($_) => 1} split(';', $ENV{LIB});
       @libpaths = grep {
          my $d = lc $_;
          $d =~ s/^"//;
          $d =~ s/"$//;
          exists($envlibs{$d}) ? 0 : 1
       } @libpaths;
    }
    setvar( 'LIBPATH', @libpaths);
    my @libs = map { s/^$LDLIBFLAG//; $_} quoted_split($Config{ libs});
    #    push ( @libs, 'pmprintf') if $OS2 && defined $ENV{PRIMA_PMPRINTF};
    if ( $useGC) {
	push @libs, 'leak';
    }
    setvar( 'LIBS', ());
    my @flibs;

# here starts lot of execs...

    for my $lib (@libs) {
       if ( defined find_lib($lib,'')) {
          push @flibs, $lib;
       } else {
          print "*** Warning (probably harmless): `$lib' library not found\n";
       }
    }
    setvar( 'LIBS', @flibs);
    %DEFINES = (
                PERL_PATCHLEVEL => $Config{ PATCHLEVEL},
                PERL_SUBVERSION => $Config{ SUBVERSION},
                PERL_POLLUTE => 1,
               );
    if ( env_true( 'PRIMA_PARANOID_MALLOC')) {
	$DEFINES{ PARANOID_MALLOC} = 1;
    }
    if ( $useGC) {
	$DEFINES{ USE_GC} = 1;
    }
    while ( $CFLAGS =~ s/-D(\w+)(?:=(\S+))?\s*//) {
        my ( $defname, $defvalue) = ( $1, $2 || 1);
        $DEFINES{ $defname} = $defvalue;
    }

    setvar( 'PREFIX', $Config{ installsitearch});
    setvar( 'INSTALL_BIN', $Config{ installbin});
    setvar( 'INSTALL_LIB', $PREFIX . qd( "/$PRJ"));
    setvar( 'INSTALL_DL', $PREFIX . qd( "/auto/$PRJ"));
    setvar( 'INSTALL_EXAMPLES', $PREFIX . qd( "/$PRJ/examples"));

    setvar( 'MAKETYPE', lc ( $MAKE = $Config{ make}));
    # Note that Borland's make utility also named 'make'. But its
    # usage is deprecated, thus we can ignore it.
    #
    # In Linux GNU make also has name 'make'. But Makefile been
    # generated by this script is compatible with it. If it named
    # 'gmake' then we force $MAKETYPE to contain 'make'.
    $MAKETYPE = 'make' if $MAKETYPE =~ /^gmake|pmake$/;
    die "Unknown make utility" unless $MAKETYPE =~ /^make|nmake|dmake$/;
    setvar( 'RM', $Config{ rm});
    setvar( 'SHQUOTE', $Win32 ? '"' : "'");
    # setvar( 'DEFFILE', $Win32 ? qd( "win32/Prima.def") : ( $OS2 ? qd( "os2/Prima.def") : ''));
    # check_dTHR() if $platform eq 'unix'; XXX
}

sub setup_paths
{
    my $libpath;
    if ( $Win32 || $OS2) {
       $libpath = find_lib( $Config{ libperl}, "", qd( "$Config{ archlib}/CORE"));
       die "Cannot find perl library" unless defined $libpath;
       print "Perl library found in ", ( $libpath || "default LIBPATH"), "\n";
       push @LIBPATH, $libpath if $libpath;
       push @LIBS, $Config{ libperl};

       $libpath = find_lib( "Prima$LIB_EXT", "", qd("$PRIMA_INSTDIR/auto/Prima"));
       die "Cannot find prima library" unless defined $libpath;
       print "Prima library found in ", ( $libpath || "default LIBPATH"), "\n";
       push @LIBPATH, $libpath if $libpath;
       push @LIBS, "Prima$LIB_EXT";
    }
}

sub setup_defines
{
 #   have_header( "io.h"); XXX
 #   have_header( "unistd.h"); XXX
 #   have_header( "strings.h"); XXX

    if ( $OS2 && defined $ENV{ PRIMA_PMPRINTF}) {
        if ( have_header( "pmprintf.h")) {
            my $libpath = find_lib( 'pmprintf', "");
            die "There is no library for pmprintf" unless defined $libpath;
            push @LIBS, 'pmprintf';
        }
        else {
            warn "PRIMA_PMPRINTF defined, but there is no pmprintf.h in include path";
        }
    }
    if ( $useGC) {
	have_header( 'gc.h');
    }

    foreach my $defname ( keys %USER_DEFINES) {
        if ( defined $USER_DEFINES{ $defname}) {
            $DEFINES{ $defname} = $USER_DEFINES{ $defname};
        }
        else {
            delete $DEFINES{ $defname} if exists $DEFINES{ $defname};
        }
    }
    foreach my $defname ( keys %USER_DEFINES_ADDONS) {
        $DEFINES{ $defname} = ( $DEFINES{ $defname} || '') . join( '', @{ $USER_DEFINES_ADDONS{ $defname}});
    }
}

sub generate_def
{
   my ( $file, $libid, @exports) = @_;
   return if !$Win32 && !$OS2;
   open PRIMADEF, ">$file" or die "Cannot create $file: $!";
   if ( $Win32) {
      print PRIMADEF <<EOF;
LIBRARY $libid
CODE LOADONCALL
DATA LOADONCALL NONSHARED MULTIPLE
EXPORTS
EOF
      if ( $COMPILER eq 'bcc32') {
          print PRIMADEF map { "\t_$_\n\t$_=_$_\n"} @exports;
      }
      else {
          print PRIMADEF map { "\t$_\n\t_$_ = $_\n"} @exports;
      }
   } else {
      $libid = dl_name( $libid);
      print PRIMADEF <<EOF;
LIBRARY $libid INITINSTANCE TERMINSTANCE
CODE LOADONCALL
DATA LOADONCALL NONSHARED MULTIPLE
EXPORTS
EOF
       print PRIMADEF map { "\t$_\n"} @exports;
   }
   close PRIMADEF;
}

sub setup_misc
{
    # Setting up translation tables for quoting string for different make utilities.
    my ( $make_tbl, $nmake_tbl, $dmake_tbl);
    $make_tbl = [ map { chr} 0..255];
    $dmake_tbl = [ map { chr} 0..255];
    $nmake_tbl = [ map { chr} 0..255];
    foreach ( ':', ';', '#', '(', ')', '^', '\\', '{', '}', '!', '@', '-') {
        $nmake_tbl->[ ord] = "^$_";
    }
    $nmake_tbl->[ ord '%'] = '%%';
    $nmake_tbl->[ ord '$'] = $make_tbl->[ ord '$'] = '$$';
    $dmake_tbl->[ ord '#'] = '\\#';
    $make_tbl->[ ord '#'] = '\\#';
    %make_trans = (
                   make => $make_tbl,
                   nmake => $nmake_tbl,
                   dmake => $dmake_tbl,
                  );
}



sub process_commandline
{
    %overrideable = map { /^(.)(.*)$/; $2 => $1} @ovvars; # Script variables which may be overridden.
    $ARGV_STR = join( " ", map { "\"$_\""} @ARGV);
    foreach my $arg ( @ARGV) {
        if ( $arg =~ /^\s*(\w+)\s*(\+?)\=(.*)$/) {
            my ( $varname, $setmode, $varval) = ( $1, $2 || '', $3);
            die "Unknown variable $varname" unless $overrideable{ $varname};
            if ( $overrideable{ $varname} eq '$') {
                if ( $setmode eq '+') {
                    push @{ $USER_VARS_ADDONS{ $varname}}, $varval;
                }
                else {
                    $USER_VARS{ $varname} = $varval;
                }
                die $@ if $@;
            }
            elsif ( $overrideable{ $varname} eq '@') {
                my @values = split /$path_sep/, $varval;
                if ( $setmode eq '+') {
                    push @{ $USER_VARS_ADDONS{ $varname}}, @values;
                }
                else {
                    $USER_VARS{ $varname} = @values;
                }
                die $@ if $@;
            }
        }
        elsif ( $arg =~ /^-(D|U)(\w+)(?:(\+?)=(.*))?$/) {
            my ( $defmode, $defname, $setmode, $value) = ( $1, $2, $3 || '', $4 || '');
            if ( $defmode eq 'U') {
                $USER_DEFINES{ $defname} = undef; # I.e. it will exists in the hash...
            }
            else {
                if ( $setmode eq '+') {
                    push @{ $USER_DEFINES_ADDONS{ $defname}}, $value;
                }
                else {
                    $USER_DEFINES{ $defname} = $value;
                }
            }
        }
        else {
            die "Unknown command line argument or wrong syntax: '$arg'";
        }
    }
}

sub find_file
{
    my ( $fname, $dir) = @_;
    my $pathname = qd( "$dir/$fname");
    if ( -e $pathname) {
        return ( $pathname, 1);
    }
    opendir D, $dir or die "Cannot open dir $dir: $!";
    my @entries = map { qd( "$dir/$_")} grep { /^[^.]/ && -d qd( "$dir/$_")} readdir D;
    closedir D;
    my $found;
    foreach my $entry ( @entries) {
        ( $pathname, $found) = find_file( $fname, $entry);
        return ( $pathname, 1) if $found;
    }
    return ( $fname, 0);
}

sub canon_name
{
    my ( $fname) = @_;
    my $qdirsep = quotemeta( $dir_sep);
    $fname =~ s{[^$qdirsep]+$qdirsep\.\.(?:$qdirsep|\Z)}{}
        while $fname =~ /(?:$qdirsep|\A)\.\.(?:$qdirsep|\Z)/;
    $fname =~ s{(?:(?<=$qdirsep)|(?<=\A))\.(?=$qdirsep|\Z)$qdirsep?}{}g;
    return $fname;
}

sub find_cdeps
{
    my ( $cfile, $deps, $included) = @_;

    $deps ||= {};
    $included ||= {};

    return () if exists $deps->{ $cfile};
    $deps->{ $cfile} = [];
    return @{ $alldeps{ $cfile}} if exists $alldeps{ $cfile};
    $alldeps{ $cfile} = [];
    return () unless -f $cfile;

    local *CF;
    open CF, "<$cfile" or die "Cannot open $cfile: $!";
    while ( <CF>) {
        chomp;
        next unless /^\s*\#\s*include\s+"([^\"]+)"/;
        my $incfile = $1;
        my $found = 0;
        if ( exists $mapheader{$incfile}) {
           $incfile = $mapheader{$incfile};
           next unless defined $incfile; # usually apricot.h
           $found = 1;
        } else {
           ( $incfile, $found) = find_file( $incfile, ".");
        }
        die "Can't find $incfile\n" unless $found;
        $incfile = canon_name( $incfile);
        unless ( exists $included->{ $incfile}) {
            push @{ $alldeps{ $cfile}}, $incfile;
            push @{ $deps->{ $cfile}}, $incfile;
            $included->{ $incfile} = 1;
        }
        my @subdeps = find_cdeps( $incfile, $deps, $included);
        push @{ $deps->{ $cfile}}, @subdeps;
        push @{ $alldeps{ $cfile}}, @subdeps;
    }
    close CF;
    return @{ $deps->{ $cfile}};
}

sub cmake
{
    my ( $subdir, $cfile) = @_;
    print "Finding dependencies for $cfile...\n";
    my $ofile = "$1$OBJ_EXT" if $cfile =~ /^(.*)\.c$/;
    my $rname = $1;
    die "Internal error: illegal c file"
        unless defined $ofile;
    $cfile = qd( $cfile);
    $ofile = qd( $ofile);
    push @allclean, $ofile;
    push @allobjects, $ofile;
    my @deps = find_cdeps( $cfile);
    local @INCPATH = @INCPATH;
    push ( @INCPATH, qd("include/generic/$subdir")) if defined $subdir;
    return(
           "$ofile: Makefile $cfile @deps\n\t" .
           join( " ", cc_command_line( $cfile, $ofile, "", 1, 1)) .
           "\n\n"
          );
}

sub clsmake
{
    my ( $subdir, $clsfile) = @_;
    print "Finding dependencies for $clsfile...";
    my $classname = $1 if $clsfile =~ /^(.*)\.cls$/;
    die "Internal error: illegal cls file"
        unless defined $classname;
    if ( defined $subdir) { $subdir = "/$subdir" } else { $subdir = '' };
    my $mk = qd( "include/generic/$classname.h"
               ) . ": Makefile " .
                   "$clsfile";
    push @allclean, (
                     qd( "include/generic/$classname.h"),
                     qd( "include/generic/$classname.inc"),
                    );
    push @allinstall, qd( "include/generic/$classname.h"), $INSTALL_LIB . qd( "/CORE/generic$subdir");
    $classname =~ m/\/*(\w*)$/;
    my $fn = $1;
    $mapheader{"$fn.h"} = "include/generic$subdir/$fn.h";
    $mapheader{"$fn.inc"} = "include/generic$subdir/$fn.inc";
    my @ancestors = gencls( $clsfile, depend => 1);
    $mk .= qd( "include/generic/$_.h $_.cls ") foreach @ancestors;
    print "\n";
    $mk .= "\n\tgencls " .
        "--inc --h $clsfile include/generic${subdir}\n\n";
}

sub dllmake
{
   my $subdir = $_[0];
   my $sbd  = defined($subdir) ? "$subdir/$subdir" : $PRJ;
   my $name = qd( "auto/$PRJ/" . dl_name( $sbd));
   push ( @allsubtargets,  $name);
   push ( @subtargetsdirs, qd( "auto/$PRJ" . ( defined($subdir) ? "/$subdir" : '')));
   push ( @allclean,      $name);
   local @LIBS = @LIBS;
   local @LIBPATH = @LIBPATH;
   if ( defined ( $subdir) && $Win32) {
      push( @LIBS,    "$PRJ$LIB_EXT");
      push( @LIBPATH, cwd() . qd("/auto/$PRJ"));
   }
      return <<HDD;
$name: @allobjects
\t${ \eval{ return join ' ', ld_command_line( $name, qd("$sbd.def"), @allobjects)}}

HDD
   
}

$Win32 = ( $^O =~ /mswin32/i);
$OS2 = ( $^O =~ /os2/i);
$dir_sep = (( ( $path_sep = $Config{ path_sep}) eq ':') || ( defined $Config{ emxpath})) ? '/' : '\\';

if ( $#ARGV >= 0 && $ARGV[ 0] =~ /^\-\-cp(bin)?$/) {
    my $isbin = defined $1;
    shift @ARGV;
    die qq(Even number of parameters expected) unless ( $#ARGV % 2);
    while ( scalar @ARGV) {
        my ( $src, $dst) = ( shift @ARGV, shift @ARGV);
        print qq(Installing $src -> $dst\n);
        next unless -f $src;
        if ( $isbin) {
            my $dstdir = dirname( $dst);
            mkpath $dstdir if $dstdir && ! -d $dstdir;
            open SRCPL, "<$src" or die "Cannot open $src: $!";
            open DSTPL, ">$dst" or die "Cannot create $dst: $!";
            chmod 0555, $dst unless $Win32 || $OS2;
            if ( $Win32) {
                print DSTPL <<EOF;
\@rem = '--*-Perl-*--
\@echo off
if "%OS%" == "Windows_NT" goto WinNT
$^X -w -x -S "%0" %1 %2 %3 %4 %5 %6 %7 %8 %9
goto endofperl
:WinNT
$^X -w -x -S "%0" %*
if NOT "%COMSPEC%" == "%SystemRoot%\\system32\\cmd.exe" goto endofperl
if %errorlevel% == 9009 echo You do not have Perl in your PATH.
goto endofperl
\@rem ';
#!perl
EOF
            }
            elsif ( $OS2) {
                my $perlpath = $Config{ perlpath};
                $perlpath =~ s/(perl)(\.exe)?$/$1__$2/i if $perlpath =~ /perl(\.exe)?$/i;
                print DSTPL <<EOF;
extproc $perlpath -wS
EOF
            }
            else {
                print DSTPL <<EOF;
#! $Config{ perlpath} -w
EOF
            }
            my $filestart = 1;
            while ( <SRCPL>) {
                next if $filestart && /^\#\!/;
                $filestart = 0;
                print DSTPL;
            }
            if ( $Win32) {
                print DSTPL <<EOF;
__END__
:endofperl
EOF
            }
            close SRCPL;
            close DSTPL;
        }
        else {
            die qq(Destination must be a directory) if -e $dst && ! -d $dst;
            mkpath $dst unless -d $dst;
            copy( $src, $dst) or die qq(Copy failed: $!);
        }
    }
}
elsif ( $#ARGV >= 0 && $ARGV[ 0] eq '--md') {
    shift @ARGV;
    mkpath \@ARGV;
}
elsif ( $#ARGV >= 0 && $ARGV[ 0] eq '--rm') {
    shift @ARGV;
    unlink @ARGV;
}
else {
   $CWD = cwd;
#  apricot.h is located either in 'installsitearch/Prima/CORE' or somewhere in $PERL5LIB/include
   $| = 1;
   print "Setting up working environment.\n";
   %USER_VARS_LINKS = (
      INSTALL_BIN => {
          src => 'PREFIX',
          format => '%s/bin',
      },
    );
    process_commandline;
    setup_variables;
    setup_paths;
    setup_defines;
    setup_misc;


    my $make = <<EOF;
# Makefile for $PRJ project under $Config{archname}
#
# THIS IS GENERATED FILE.
#
# Do not edit -- all changes will be lost.
# Edit Makefile.PL instead.

all: project

EOF

if ( $ENV{ HOME} && ( -d qd( "$ENV{ HOME}/.Makeinc"))) {
    my $hdir = "$ENV{ HOME}/.Makeinc";
    if ( -f "$hdir/header.PL") {
        push @headers, `$^X $hdir/header.PL`;
        push @Makefile_deps, "$hdir/header.PL";
    }
    if ( -f "$hdir/footer.PL") {
        push @footers, `$^X $hdir/footer.PL`;
        push @Makefile_deps, "$hdir/footer.PL";
    }
}

 $make .= join( "\n", @headers) . "\n";

opendir D, '.' or die "Can't open current directory\n";
my @d = readdir D;
closedir D;

{
   local @allobjects = @allobjects;
   my $touch = 0;

   while ( <*.cls>) {
       $make .= clsmake( undef, $_);
       $touch = 1;
   }
   while ( <*.c>) {
       $make .= cmake( undef, $_);
       $touch = 1;
   }
   $make .= dllmake() if $touch;
   generate_def("$PRJ.def", $PRJ, "boot_$PRJ", "create_compatible_image");
}

my @prjdirs;

if (1) {
for ( @d) {
   next unless -d $_;
   next if m/^\./;
   local @allobjects = @allobjects;
   my $subdir = $_;
   my $touch = 0;
   while ( <$subdir/*.cls>) {
      $make .= clsmake( $subdir, $_);
      $touch = 1;
   }
   while ( <$subdir/*.c>) {
       $make .= cmake( $subdir, $_);
       $touch = 1;
   }
   next unless $touch;
   push( @prjdirs, $subdir);
   $make .= dllmake( $subdir);
   generate_def("$subdir/$subdir.def", $subdir, "boot_${PRJ}__$subdir");
}}

    push @allrealclean, 'Makefile';
    push @alldirs, qd( "include/generic"), qd( "auto/$PRJ");
    push @alldirs, map {qd "include/generic/$_"} @prjdirs;
    push @alldirs, map {qd "auto/$PRJ/$_"} @prjdirs;
    
    print "Writing Makefile...";
    $make =~ tr[/][]s;
    open MAKE, ">Makefile" or die "Creation of Makefile failed: $!";
    print MAKE $make;

    print MAKE <<EOF;

RM=$^X Makefile.PL --rm

clean:
\t\$(RM) @allclean 

realclean: clean
\t\$(RM) @allrealclean

project: dirs @allsubtargets 

Makefile: Makefile.PL @Makefile_deps
\t\@echo Rebuilding Makefile...
\t\@$^X Makefile.PL $ARGV_STR
\t\@$MAKE
\t\@echo You are safe to ignore the following error...
\t\@false

CP=$^X Makefile.PL --cp
CPBIN=$^X Makefile.PL --cpbin

install: all
\t\@\$(CP) \\
EOF

$make = "";
my $i;
for ( $i = 0; $i < @allsubtargets; $i++) {
   $make .= "\t$allsubtargets[$i] $PREFIX/$subtargetsdirs[$i] \\\n";
   if ( $Win32) { # libs additional to dll
      my $libt = $allsubtargets[$i];
      if ( $libt =~ s/\.dll$/.lib/i) {
         $make .= "\t$libt $PREFIX/$subtargetsdirs[$i] \\\n";   
      }
   }
}
print MAKE $make;

    my ( $instcnt, $maxcnt) = ( 1, 20);
    for ( @prjdirs) {
       my $dir = $_;
       while ( <$dir/*.pm>) { 
          print MAKE "\t", qd( "$_ $PREFIX/$PRJ"); 
          print MAKE "\n\t\@\$(CP)";
       }
    }          
    while ( scalar @allinstall) {
        my ( $src, $dst) = ( shift @allinstall, shift @allinstall);
        print MAKE "\t$src $dst";
        if ( ( $instcnt % $maxcnt) == 0) {
            print MAKE "\n\t\@\$(CP)";
        }
        $instcnt++;
        print MAKE " \\\n";
    }
    while ( <include/*.h>) {
        my ( $dsth) = $_;
        $dsth =~ s/^include/CORE/;
        print MAKE "\t$_ ", dirname( qd( "$INSTALL_LIB/$dsth"));
        if ( ( $instcnt % $maxcnt) == 0) {
            print MAKE "\n\t\@\$(CP)";
        }
        $instcnt++;
        print MAKE " \\\n";
    }
    while ( <include/$PLATFORM/*.h>) {
        my ( $dsth) = $_;
        $dsth =~ s/^include/CORE/;
        print MAKE "\t$_ ", dirname( qd( "$INSTALL_LIB/$dsth"));
        if ( ( $instcnt % $maxcnt) == 0) {
            print MAKE "\n\t\@\$(CP)";
        }
        $instcnt++;
        print MAKE " \\\n";
    }
    print MAKE "\t$PRJ.pm $PREFIX\n";

    print MAKE "\t\@\$(CPBIN)";
    $instcnt = 1;
    while ( scalar @allbins) {
        my ( $src, $dst) = ( shift @allbins, shift @allbins);
        if ( ( $instcnt % $maxcnt) == 0) {
            print MAKE "\n\t\@\$(CPBIN)";
        }
        $dst = qd( "$dst/" . basename( $src, '.pl')) . $SCRIPT_EXT;
        print MAKE " \\\n\t$src $dst"
    }
    print MAKE "\n";

    print MAKE <<EOF;

MD=$^X Makefile.PL --md

dirs: @alldirs

@alldirs:
\t\@echo Creating directories...
\t\@\$(MD) @alldirs

EOF

    print MAKE join( "\n", @footers), "\n";

    close MAKE;
    print "\nAll done.  Now you can run ${ MAKE}.\n";
}

exit;

